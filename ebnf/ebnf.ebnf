circuit = device_list , connect_list , monitor_list , "//";

device_list = "DEVICES" , "[" , device , {"," , device} , "]";
connect_list = "CONNECTIONS" , "[" , device_connections , {"," , device_connections} , "]";
monitor_list = "MONITORS" , "[" , ( name | "all" ) , {"," , name} , ";" "]";

device = name , {"," , name} , "=" , devices_type , ";";
device_connections = "device" , name , "{" , connection , {connection} , "}";
connection = start_con , "->" , end_con , ";"; 

devices_type = ( dtype | xor_gate | single_gate | clock | switch );
dtype = "DTYPE"; 
single_gate = ("NAND" | "NOR" | "AND" | "OR") , digit , [digit], ("input" | "inputs");
xor_gate = "XOR";
clock = "CLOCK" , digit , ("cycle" | "cycles");
switch = "SWITCH" , ("0" | "1");

start_con = name , ["." , ("QBAR" | "Q")];
end_con = name , "." , (( "I" , digit , [digit] ) | "SET" | "CLK" | "CLEAR" | "DATA" );

name = letter , {letter | digit};